import java.time.Instant

buildscript {
  repositories {
    jcenter()
  }
  dependencies {
    classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.3.1'
  }
}

subprojects { project ->
  apply plugin: 'java'
  apply plugin: 'com.jfrog.bintray'

  group = 'com.palominolabs.metrics'
  version = '1.1.1'
  ext.isReleaseVersion = !version.endsWith('SNAPSHOT')
  ext.deps = [jackson: '2.6.1',
              junit  : '4.12']

  sourceCompatibility = JavaVersion.VERSION_1_6

  repositories {
    jcenter()
  }

  task sourcesJar(type: Jar, dependsOn: classes) {
    from sourceSets.main.allJava
    classifier = 'sources'
  }

  task javadocJar(type: Jar, dependsOn: javadoc) {
    from javadoc.destinationDir
    classifier = 'javadoc'
  }

  artifacts {
    // register in 'archives' config so they will be signed
    archives sourcesJar
    archives javadocJar
  }

  project.bintray {
    user = rootProject.hasProperty('bintrayUser') ? rootProject.property('bintrayUser') : 'FIXME'
    key = rootProject.hasProperty('bintrayApiKey') ? rootProject.property('bintrayApiKey') : 'FIXME'
    configurations = ['archives']

    pkg {
      repo = 'maven'
      licenses = ['WTFPL']
      vcsUrl = 'https://github.com/palominolabs/metrics-new-relic'
      name = 'metrics-new-relic'

      version {
        name = project.version
        released = Date.from(Instant.now())
        if (project.isReleaseVersion) {
          vcsTag = 'v' + project.version
        }
      }
    }
  }
}


task wrapper(type: Wrapper) {
  gradleVersion = '2.6'
}
